description = """\
Smack for Android.
All the required dependencies to run Smack on Android.
Usually you want to add additional dependencies like smack-tcp,
smack-extensions and smack-experimental."""

// Note that the test dependencies (junit, â€¦) are inferred from the
// sourceSet.test of the core subproject
dependencies {
	// androidProjects lists all projects that are checked to compile against android.jar
	// Filter out the optional Smack dependencies from androidProjects
	(androidProjects - androidOptionalProjects)
	.each { project ->
		compile project
	}

	// Add the Android jar to the Eclipse .classpath.
	compile files(androidBootClasspath)
}

configure (androidProjects) {
	task compileAndroid(type: JavaCompile) {
		source = compileJava.source
		classpath = compileJava.classpath
		destinationDir = new File(buildDir, 'android')
		options.bootClasspath = androidBootClasspath
	}
}

test { dependsOn androidProjects*.compileAndroid }
